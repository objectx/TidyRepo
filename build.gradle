apply plugin: "groovy"
apply plugin: "application"

mainClassName = 'Main'

defaultTasks 'build'

repositories {
    // Spock releases are available from Maven Central
    mavenCentral ()
    // Spock snapshots are available from the Sonatype OSS snapshot repository
    maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
    jcenter ()
}

dependencies {
    // mandatory dependencies for using Spock
    compile "org.codehaus.groovy:groovy-all:2.4.3:indy"
    compile "commons-cli:commons-cli:1.2"
    compile "org.slf4j:slf4j-api:1.7.10"
    compile "ch.qos.logback:logback-classic:1.1.2"

    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
}


tasks.withType (GroovyCompile) {
    groovyOptions.optimizationOptions.indy = true
}

task uberjar (type: Jar, dependsOn: [':assemble']) {
    classifier = 'deploy'
    from files (sourceSets.main.output.classesDir)
    from files (sourceSets.main.output.resourcesDir)
    from configurations.runtime.asFileTree.files.collect { zipTree (it) }

    entryCompression = ZipEntryCompression.DEFLATED
    zip64 = true
    manifest {
        attributes 'Main-Class': 'Main'
    }
}

task go (type: JavaExec, dependsOn: [':classes']) {
    args = ['-N', '-v']
    classpath = sourceSets.main.runtimeClasspath
    main = 'Main'
}

